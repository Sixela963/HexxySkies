buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Make sure this version matches the one included in Kotlin for Forge
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22'
        // OPTIONAL Gradle plugin for Kotlin Serialization
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.9.22'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    // Mavens for forge exclusive dependencies
    maven {
        name = 'Forge Maven'
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
    maven {
        name = "C4's Maven"
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {
    forge("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    include(modApi("dev.architectury:architectury-forge:${rootProject.architectury_api_version}"))
    implementation(include((("io.github.llamalad7:mixinextras-forge:0.4.1"))))

    //VS2 Dependencies. The latest VS2 and VSCore versions can be retrieved from the Valkyrien Skies Github repository.
    //The version tag is usually the major version, followed by the first 10 characters of the git commit hash.
    //The VSCore version tag can be grabbed directly from the gradle.properties file in the Valkyrien skies repo.
    modCompileOnly("org.valkyrienskies:valkyrienskies-120-forge:${rootProject.vs2_version}") { transitive = false
        exclude group: 'org.valkyrienskies.core', module: ''
        exclude group: "com.simibubi"
        exclude group: "io.github.fabricators_of_create"
        exclude group: "dev.engine-room"
        exclude group: "dev.engine_room"
    }
    implementation("org.valkyrienskies.core:api:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:util:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:internal:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    modRuntimeOnly files("${rootDir}\\libs\\valkyrienskies-120-forge-2.4.0+ab9d3d62ee.jar")

    // Kotlin
    implementation("thedarkcolour:kotlinforforge:${rootProject.kotlin_forge}")

    compileOnly("org.joml:joml:1.10.4")
    compileOnly("org.joml:joml-primitives:1.10.0")

    modImplementation("at.petra-k.hexcasting:hexcasting-forge-${minecraft_version}:${hexcasting_version}") { transitive = false }
    modCompileOnly "at.petra-k.paucal:paucal-forge-${minecraft_version}:${paucal_version}"
    modRuntimeOnly "at.petra-k.paucal:paucal-forge-${minecraft_version}:${paucal_version}"
    modCompileOnly "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FORGE-SNAPSHOT:api"
    modRuntimeOnly "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FORGE-SNAPSHOT"
    modRuntimeOnly ("com.samsthenerd.inline:inline-forge:$minecraft_version-$inline_version") { transitive = false }
    modCompileOnly ("top.theillusivec4.caelus:caelus-forge:${caelus_version}+1.20.1:api") { transitive = false }
    modRuntimeOnly ("top.theillusivec4.caelus:caelus-forge:${caelus_version}+1.20.1") { transitive = false }
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": version, "mod_id": mod_id, "mod_name": mod_name,
        "mod_description": mod_description, "license": license, "source": mod_source,
        "hexcasting_version": hexcasting_version, "vs2_version": vs2_version.contains('+')
                ? vs2_version.substring(0, vs2_version.indexOf('+')) : vs2_version
    }
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        mixinConfig "hexxyskies-common.mixins.json"
        mixinConfig "hexxyskies.mixins.json"
        convertAccessWideners.set(true)
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
    mixin {
        defaultRefmapName = "hexxyskies-refmap.json"
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}


publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
