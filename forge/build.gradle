plugins {
    id 'com.gradleup.shadow'
    id "me.modmuss50.mod-publish-plugin"
}

loom {
    forge {
        mixinConfig "hexxyskies.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
    maven {
        name = "C4's Maven"
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    implementation(include((("io.github.llamalad7:mixinextras-forge:0.4.1"))))

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation("org.valkyrienskies:valkyrienskies-120-forge:$vs2_version") { transitive = false
        exclude group: 'org.valkyrienskies.core', module: ''
        exclude group: "com.simibubi"
        exclude group: "io.github.fabricators_of_create"
        exclude group: "dev.engine-room"
        exclude group: "dev.engine_room"
    }
    implementation("org.valkyrienskies.core:api:$vscore_version") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:util:$vscore_version") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:internal:$vscore_version") { transitive = false
        exclude group: 'org.joml', module: ''
    }

    // Kotlin
    implementation("thedarkcolour:kotlinforforge:$kotlin_version")

    compileOnly("org.joml:joml:1.10.4")
    compileOnly("org.joml:joml-primitives:1.10.0")

    modImplementation("at.petra-k.hexcasting:hexcasting-forge-$minecraft_version:$hexcasting_version") { transitive = false }
    modCompileOnly "at.petra-k.paucal:paucal-forge-$minecraft_version:$paucal_version"
    modRuntimeOnly "at.petra-k.paucal:paucal-forge-$minecraft_version:$paucal_version"
    modCompileOnly "vazkii.patchouli:Patchouli:$minecraft_version-$patchouli_version-FORGE-SNAPSHOT:api"
    modRuntimeOnly "vazkii.patchouli:Patchouli:$minecraft_version-$patchouli_version-FORGE-SNAPSHOT"
    modRuntimeOnly ("com.samsthenerd.inline:inline-forge:$minecraft_version-$inline_version") { transitive = false }
    modCompileOnly ("top.theillusivec4.caelus:caelus-forge:$caelus_version+1.20.1:api") { transitive = false }
    modRuntimeOnly ("top.theillusivec4.caelus:caelus-forge:$caelus_version+1.20.1") { transitive = false }

    modRuntimeOnly("ram.talia.moreiotas:moreiotas-forge-$minecraft_version:$moreiotas_version") { transitive = false }
    modRuntimeOnly("maven.modrinth:complex-hex:$complexhex_version")
    modRuntimeOnly("ram.talia.hexal:hexal-forge-$minecraft_version:$hexal_version") { transitive = false }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version, hexcasting_version: hexcasting_version, moreiotas_version: moreiotas_version,
        hexal_version: hexal_version, hexodus_version: hexodus_version, architectury_api_version: architectury_api_version,
        complexhex_version: (complexhex_version.contains("-") ? complexhex_version.substring(0, complexhex_version.indexOf("-")) : complexhex_version),
        vs2_version: (vs2_version.contains("+") ? vs2_version.substring(0, vs2_version.indexOf("+")) : vs2_version), hexical_version: hexical_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("forge")

    version = project.version
    displayName = "Hexxy Skies $project.version"

    curseforge {
        accessToken = System.getenv("CURSEFORGE_TOKEN") == null ? "" : System.getenv("CURSEFORGE_TOKEN")
        projectId = "1426252"
        minecraftVersions.add(minecraft_version)

        requires("fabric-api", "architectury-api", "kotlin-for-forge", "hexcasting")
        optional("complex-hex", "moreiotas")
    }

    modrinth {
        accessToken = System.getenv("MODRINTH_TOKEN") == null ? "" : System.getenv("MODRINTH_TOKEN")
        projectId = "ta01udVD"
        minecraftVersions.add(minecraft_version)

        requires("architectury-api")
        requires("kotlin-for-forge")
        requires("hex-casting")
        optional("complex-hex")
        optional("moreiotas")
    }
}