plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven { url = "https://maven.terraformersmc.com/releases" }
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }

    // dependencies
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")
    include(annotationProcessor(implementation("io.github.llamalad7:mixinextras-fabric:0.4.1")))

    include(modApi("dev.architectury:architectury-fabric:${rootProject.architectury_api_version}"))

    //VS2
    modCompileOnly("org.valkyrienskies:valkyrienskies-120-fabric:${rootProject.vs2_version}") {
        exclude group: "com.simibubi.create"
        exclude group: "net.createmod.ponder"
        // Excluding sodium since it crashes with vs2 for some reason
        exclude group: 'maven.modrinth', module: 'sodium'
        exclude group: "io.github.fabricators_of_create.PortingLib"
        exclude group: "com.tterrag.registrate_fabric"
        exclude group: "dev.engine-room.flywheel"
    }
    implementation("org.valkyrienskies.core:api:${rootProject.vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:util:${rootProject.vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:internal:${rootProject.vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    modRuntimeOnly files("${rootDir}\\libs\\valkyrienskies-120-fabric-2.4.0+ab9d3d62ee.jar")

    // Kotlin
    modImplementation("net.fabricmc:fabric-language-kotlin:${rootProject.kotlin_fabric}")

    api "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"
    api "org.jetbrains.kotlin:kotlin-reflect:2.0.0"


    implementation("org.joml:joml-primitives:1.10.0")
    implementation("org.joml:joml:1.10.4")

    modImplementation "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FABRIC"
    modImplementation "at.petra-k.hexcasting:hexcasting-fabric-${minecraft_version}:${hexcasting_version}"
    modRuntimeOnly "at.petra-k.paucal:paucal-fabric-${minecraft_version}:${paucal_version}"
    modRuntimeOnly "com.samsthenerd.inline:inline-fabric:$minecraft_version-$inline_version"
    modRuntimeOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}"
    modRuntimeOnly "io.github.tropheusj:serialization-hooks:0.4.99999"
    modImplementation("software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}


processResources {
    // set up properties for filling into metadata
    Map<String, Object> properties = new HashMap<>()
    properties.put("version", version)
    properties.put("fabric_loader_version", fabric_loader_version)
    properties.put("fabric_api_version", fabric_api_version)
    properties.put("minecraft_version", minecraft_version)
    properties.put("mod_id", mod_id)
    properties.put("mod_name", mod_name)
    properties.put("mod_description", mod_description)
    properties.put("license", license)
    properties.put("source", mod_source)
    properties.put("hexcasting_version", hexcasting_version)
    properties.put("vs2_version", vs2_version.contains('+') ? vs2_version.substring(0, vs2_version.indexOf('+')) : vs2_version)
    properties.forEach(inputs::property)

    filesMatching("fabric.mod.json") {
        expand properties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
